allprojects {
    repositories {
        jcenter()
        maven { url "https://jitpack.io" }
    }
}

buildscript {
    repositories {
        jcenter()
        mavenCentral()
    }

    dependencies {
        classpath 'com.android.tools.build:gradle:2.1.2'
        classpath 'org.ajoberstar:gradle-git:0.2.3'
    }
}

import org.ajoberstar.gradle.git.tasks.*

task Clone(type: GitClone) {
    def destination = file("cjdns")
    uri = "https://github.com/cjdelisle/cjdns"
    destinationPath = destination
    bare = false
    enabled = !destination.exists() //to clone only once
}

task BuildCJDNS(type: Exec) {
    workingDir file("cjdns")
    commandLine file("cjdns/android_do")

    standardOutput = new ByteArrayOutputStream()
    errorOutput = standardOutput
    ignoreExitValue = true
    doLast {
        if (execResult.exitValue != 0) {
            println(standardOutput.toString())
            throw new GradleException("Exec failed! See output above.")
        }
    }
}

task CopyT(type: Copy) {
    from 'cjdns/build_android/out/*'
    into 'src/main/assets/'
}

apply plugin: 'com.android.application'
apply plugin: 'checkstyle'

android {
    buildToolsVersion '24'
    compileSdkVersion 24
    defaultConfig {
        minSdkVersion 9
        targetSdkVersion 24
        versionCode 1
        versionName "1.0.0-SNAPSHOT"
    }
    sourceSets.main {
        jni.srcDirs = []
        jniLibs.srcDir 'src/main/libs'
    }
    signingConfigs {
        release
    }
    buildTypes {
        release {
            signingConfig signingConfigs.release
        }
    }
    lintOptions {
        disable 'InvalidPackage'
    }

}

afterEvaluate {
  android.applicationVariants.all { variant ->
    variant.javaCompiler.dependsOn(Clone)
  }
}

Clone.finalizedBy(BuildCJDNS)
BuildCJDNS.finalizedBy(CopyT)

dependencies {
    compile 'com.android.support:support-v4:24.0.0'
    compile 'com.android.support:appcompat-v7:24.0.0'
    compile 'com.android.support:cardview-v7:24.0.0'
    compile 'com.android.support:recyclerview-v7:24.0.0'
    compile 'com.android.support:preference-v7:24.0.0'
    compile 'com.android.support:preference-v14:24.0.0'
    compile 'com.jakewharton:butterknife:6.0.0'
    compile 'com.joanzapata.android:android-iconify:1.0.9'
    compile('com.github.afollestad.material-dialogs:core:0.8.5.3@aar') {
        transitive = true
    }
    compile 'com.melnykov:floatingactionbutton:1.1.0'
    compile 'com.github.brnunes:swipeablerecyclerview:1.0.1'
    compile 'io.reactivex:rxjava:1.0.7'
    compile 'io.reactivex:rxandroid:0.24.0'
    compile 'com.squareup:otto:1.3.5'
    compile 'com.squareup.dagger:dagger:1.2.2'
    provided 'com.squareup.dagger:dagger-compiler:1.2.2'
}

if (project.hasProperty('keyAlias')) {
    android.signingConfigs.release.keyAlias = keyAlias
}
if (project.hasProperty('keyPassword')) {
    android.signingConfigs.release.keyPassword = keyPassword
}
if (project.hasProperty('storeFile')) {
    android.signingConfigs.release.storeFile = file(storeFile)
}
if (project.hasProperty('storePassword')) {
    android.signingConfigs.release.storePassword = storePassword
}
